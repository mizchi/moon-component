# Rust-only tasks for moon-component (optional)

moon_component := "tools/moon-component/target/release/moon-component"

default:
    @just --list --justfile justfile.rust

# Build the Rust CLI
build-cli:
    cargo build --release --manifest-path tools/moon-component/Cargo.toml

# Format Rust CLI
fmt-rust:
    cargo fmt --manifest-path tools/moon-component/Cargo.toml

# Check Rust CLI
check-rust:
    cargo check --manifest-path tools/moon-component/Cargo.toml

# Clean Rust CLI build artifacts
clean-rust:
    cargo clean --manifest-path tools/moon-component/Cargo.toml

# Build rust-guest (reference implementation)
build-rust-guest:
    #!/usr/bin/env bash
    export RUSTUP_HOME="$HOME/.rustup"
    export CARGO_HOME="$HOME/.cargo"
    export PATH="$CARGO_HOME/bin:$RUSTUP_HOME/toolchains/stable-aarch64-apple-darwin/bin:$PATH"
    cargo build --release --target wasm32-unknown-unknown --manifest-path examples/tests/rust-guest/Cargo.toml
    wasm-tools component new examples/tests/rust-guest/target/wasm32-unknown-unknown/release/rust_guest.wasm -o examples/tests/rust-guest/rust-guest.component.wasm

# Test rust-guest with rust-host
test-rust-guest: build-rust-guest
    cargo run --release --manifest-path examples/host/rust/Cargo.toml -- types examples/tests/rust-guest/rust-guest.component.wasm

# Build zig-guest (Zig implementation using C bindings)
build-zig-guest:
    zig build -p examples/tests/zig-guest/zig-out --build-file examples/tests/zig-guest/build.zig
    wasm-tools component new examples/tests/zig-guest/zig-out/bin/zig-guest.wasm \
        --adapt examples/tests/zig-guest/adapters/wasi_snapshot_preview1.reactor.wasm \
        -o examples/tests/zig-guest/zig-guest.component.wasm

# Test zig-guest with rust-host
test-zig-guest: build-zig-guest
    cargo run --release --manifest-path examples/host/rust/Cargo.toml -- types examples/tests/zig-guest/zig-guest.component.wasm

# Run Rust host tests
test-rust-host test_type="types":
    cd examples/host/rust && cargo run --release -- {{test_type}}

# Run all integration tests (Rust host + non-Rust hosts)
test-integration: test-rust-host
    just test-integration

# Run all integration tests including Scala and jco (requires sbt, pnpm)
test-integration-all: test-rust-host
    just test-integration-all
